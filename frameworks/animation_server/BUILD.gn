# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/graphic/graphic_2d/graphic_config.gni")

## Build libanimation_service.so {{{
config("libanimation_service_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "client/include",
    "//utils/system/safwk/native/include",  # system_ability_definition.h
  ]

  cflags = [
    "-Wall",
    "-Werror",
    "-g3",
  ]
}

config("libanimation_service_public_config") {
  include_dirs = [
    "export",
    "//foundation/graphic/graphic_2d/interfaces/inner_api/common",
  ]
}

ohos_shared_library("libanimation_service") {
  sources = [
    "client/src/animation_service_proxy.cpp",
    "client/src/ianimation_service.cpp",
  ]

  configs = [ ":libanimation_service_config" ]

  public_configs = [ ":libanimation_service_public_config" ]

  deps = [
    "//foundation/distributedschedule/samgr/interfaces/innerkits/samgr_proxy:samgr_proxy",
    "//foundation/graphic/graphic_2d/utils:libgraphic_utils",
  ]

  public_deps = [
    "//foundation/communication/ipc/interfaces/innerkits/ipc_core:ipc_core",
    "//utils/native/base:utils",
  ]

  part_name = "graphic_graphic_2d"
  subsystem_name = "graphic"
}

## Build libanimation_service.so }}}

## Build animation_server {{{
config("animation_server_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "export",
    "server/include",
    "//utils/system/safwk/native/include",  # system_ability_definition.h
  ]

  cflags = [
    "-Wall",
    "-Werror",
    "-g3",
  ]

  if (!ace_enable_gpu) {
    cflags += [ "-Wno-unused-private-field" ]
  }
}

ohos_executable("animation_server") {
  install_enable = true

  sources = [
    "server/src/animation_server.cpp",
    "server/src/animation_service_stub.cpp",
    "server/src/main.cpp",
  ]

  if (ace_enable_gpu) {
    sources += [
      "server/src/animation_module.cpp",
      "server/src/rotation_animation.cpp",
    ]
  } else {
    sources += [
      "server/src/mock/animation_module.cpp",
      "server/src/mock/rotation_animation.cpp",
    ]
  }

  configs = [ ":animation_server_config" ]

  deps = [
    "//foundation/communication/ipc/interfaces/innerkits/ipc_core:ipc_core",
    "//foundation/distributedschedule/samgr/interfaces/innerkits/samgr_proxy:samgr_proxy",
    "//foundation/graphic/graphic_2d:libvsync_client",
    "//foundation/graphic/graphic_2d:libwmclient",
    "//foundation/graphic/graphic_2d:libwmservice",
    "//foundation/graphic/graphic_2d/utils:libgraphic_utils",
  ]

  part_name = "graphic_graphic_2d"
  subsystem_name = "graphic"
}

## Build animation_server }}}

ohos_prebuilt_etc("cursor.raw") {
  source = "server/cursor.raw"
  part_name = "graphic_graphic_2d"
  subsystem_name = "graphic"
}
