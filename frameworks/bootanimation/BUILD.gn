# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/graphic/graphic_2d/graphic_config.gni")

## Build bootanimation {{{
config("bootanimation_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "include",
    "$graphic_2d_root/utils/log",
    "//third_party/cJSON",
    "//foundation/multimedia/player_framework/interfaces/inner_api/native",
  ]

  cflags = [
    "-Wall",
    "-Werror",
    "-g3",
  ]

  defines = []
  defines += gpu_defines
}

ohos_executable("bootanimation") {
  install_enable = true

  sources = [
    "src/boot_animation.cpp",
    "src/boot_animationconfig.cpp",
    "src/boot_videoplayer.cpp",
    "src/main.cpp",
    "src/util.cpp",
  ]

  configs = [
    ":bootanimation_config",
    "//commonlibrary/c_utils/base:utils_config",
  ]

  deps = [
    "$graphic_2d_root:libsurface",
    "$graphic_2d_root/rosen/modules/2d_graphics:2d_graphics",
    "$graphic_2d_root/rosen/modules/render_service_base/src/platform:platform",
    "$graphic_2d_root/rosen/modules/render_service_client:librender_service_client",
    "//base/customization/config_policy/frameworks/config_policy:configpolicy_util",
    "//base/global/resource_management/frameworks/resmgr:global_resmgr",
    "//base/hiviewdfx/hilog/interfaces/native/innerkits:libhilog",
    "//foundation/graphic/graphic_2d:libsurface",
    "//foundation/graphic/graphic_2d/rosen/modules/2d_graphics:2d_graphics",
    "//foundation/graphic/graphic_2d/rosen/modules/render_service_base/src/platform:platform",
    "//foundation/graphic/graphic_2d/rosen/modules/render_service_base/src/render_backend:render_backend",
    "//foundation/graphic/graphic_2d/rosen/modules/render_service_client:librender_service_client",
    "//foundation/multimedia/player_framework/interfaces/inner_api/native:media_client",
    "//foundation/systemabilitymgr/samgr/interfaces/innerkits/samgr_proxy:samgr_proxy",
    "//foundation/window/window_manager/dm:libdm",
    "//foundation/window/window_manager/wm:libwm",
    "//third_party/cJSON:cjson_static",
    "//third_party/zlib:libz",
  ]

  if (defined(use_new_skia) && use_new_skia) {
    deps += [ "//third_party/skia:skia_ohos" ]
  } else {
    deps += [ "//third_party/flutter/build/skia:ace_skia_ohos" ]
  }

  external_deps = [
    "hitrace_native:hitrace_meter",
    "ipc:ipc_core",
  ]

  if (is_standard_system) {
    external_deps += [ "init:libbegetutil" ]
  } else {
    external_deps += [ "startup:syspara" ]
  }

  part_name = "graphic_standard"
  subsystem_name = "graphic"
}

## Build bootanimation }}}

## Install boot animation resource to /system/etc/graphic/ {{{
ohos_prebuilt_etc("bootanimation_pics") {
  source = "data/bootpic.zip"
  relative_install_dir = "graphic"
  part_name = "graphic_standard"
  subsystem_name = "graphic"
}

ohos_prebuilt_etc("bootanimation_sounds") {
  source = "data/bootsound.wav"
  relative_install_dir = "graphic"
  part_name = "graphic_standard"
  subsystem_name = "graphic"
}

ohos_prebuilt_etc("bootanimation_video") {
  source = "data/bootvideo.mp4"
  relative_install_dir = "graphic"
  part_name = "graphic_standard"
  subsystem_name = "graphic"
}
## Install boot animation resource to /system/etc/graphic/ }}}
