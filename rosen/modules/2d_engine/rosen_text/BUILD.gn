# Copyright (c) 2022 Huawei Device Co., Ltd.. All rights reserved.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")

config("rosen_text_public_config") {
  include_dirs = [
    "export",
    "//foundation/graphic/graphic_2d/rosen/modules/2d_graphics/include",
    "//foundation/graphic/graphic_2d/rosen/modules/2d_graphics/src",
  ]
}

ohos_shared_library("rosen_text") {
  public_deps = [ ":rosen_text_inner" ]

  platform = current_os
  if (platform == "mingw") {
    platform = "windows"
  }

  deps = [
    "//foundation/graphic/graphic_2d/rosen/modules/2d_graphics:2d_graphics",
  ]

  part_name = "graphic_standard"
  subsystem_name = "graphic"
}

ohos_source_set("rosen_text_inner") {
  public_deps = [ ":rosen_text_texgine" ]
}

ohos_source_set("rosen_text_texgine") {
  public_configs = [ ":rosen_text_public_config" ]

  include_dirs = [
    "//foundation/graphic/graphic_2d/rosen/modules/2d_graphics/src/drawing",
    "//foundation/graphic/graphic_2d/rosen/modules/2d_graphics/src/drawing/engine_adapter",
  ]

  sources = [
    "adapter/common/text_style.cpp",
    "adapter/common/typography_style.cpp",
    "adapter/texgine/convert.cpp",
    "adapter/texgine/font_collection.cpp",
    "adapter/texgine/typography.cpp",
    "adapter/texgine/typography_create.cpp",
  ]

  deps = [ "//foundation/graphic/graphic_2d/rosen/modules/texgine:libtexgine" ]

  part_name = "graphic_standard"
  subsystem_name = "graphic"
}

ohos_source_set("rosen_text_txt") {
  public_configs = [ ":rosen_text_public_config" ]

  include_dirs = [
    "//foundation/graphic/graphic_2d/rosen/modules/2d_graphics/src/drawing",
    "//foundation/graphic/graphic_2d/rosen/modules/2d_graphics/src/drawing/engine_adapter",
  ]

  sources = [
    "adapter/common/text_style.cpp",
    "adapter/common/typography_style.cpp",
    "adapter/txt/convert.cpp",
    "adapter/txt/font_collection.cpp",
    "adapter/txt/typography.cpp",
    "adapter/txt/typography_create.cpp",
  ]

  platform = current_os
  if (platform == "mingw") {
    platform = "windows"
  }

  deps = [ "//third_party/flutter/build/skia:ace_skia_$platform" ]
  if (platform == "ohos") {
    deps += [ "//foundation/graphic/graphic_2d/rosen/build/flutter/libtxt:thirdparty_lib_txt_rosen_$platform" ]
  } else {
    deps +=
        [ "//third_party/flutter/build/libtxt:thirdparty_lib_txt_$platform" ]
  }

  part_name = "graphic_standard"
  subsystem_name = "graphic"
}
