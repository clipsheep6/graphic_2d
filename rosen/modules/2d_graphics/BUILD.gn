# Copyright (c) 2022 Huawei Device Co., Ltd.. All rights reserved.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")
import("//foundation/graphic/graphic_2d/graphic_config.gni")
import(
    "//foundation/graphic/graphic_2d/rosen/modules/2d_engine/rosen_text/config.gni")

template("graphics2d_source_set") {
  forward_variables_from(invoker, "*")

  ohos_source_set(target_name) {
    defines += invoker.defines
    cflags_cc += invoker.cflags_cc
    sources = [
      "src/c/drawing_bitmap.cpp",
      "src/c/drawing_brush.cpp",
      "src/c/drawing_canvas.cpp",
      "src/c/drawing_color.cpp",
      "src/c/drawing_font_collection.cpp",
      "src/c/drawing_path.cpp",
      "src/c/drawing_pen.cpp",
      "src/c/drawing_text_typography.cpp",
      "src/draw/brush.cpp",
      "src/draw/color.cpp",
      "src/draw/core_canvas.cpp",
      "src/draw/path.cpp",
      "src/draw/pen.cpp",
      "src/effect/color_filter.cpp",
      "src/effect/color_space.cpp",
      "src/effect/filter.cpp",
      "src/effect/image_filter.cpp",
      "src/effect/mask_filter.cpp",
      "src/effect/path_effect.cpp",
      "src/effect/shader_effect.cpp",
      "src/engine_adapter/impl_factory.cpp",
      "src/engine_adapter/skia_adapter/skia_bitmap.cpp",
      "src/engine_adapter/skia_adapter/skia_camera.cpp",
      "src/engine_adapter/skia_adapter/skia_canvas.cpp",
      "src/engine_adapter/skia_adapter/skia_color_filter.cpp",
      "src/engine_adapter/skia_adapter/skia_color_space.cpp",
      "src/engine_adapter/skia_adapter/skia_image.cpp",
      "src/engine_adapter/skia_adapter/skia_image_filter.cpp",
      "src/engine_adapter/skia_adapter/skia_impl_factory.cpp",
      "src/engine_adapter/skia_adapter/skia_mask_filter.cpp",
      "src/engine_adapter/skia_adapter/skia_matrix.cpp",
      "src/engine_adapter/skia_adapter/skia_paint.cpp",
      "src/engine_adapter/skia_adapter/skia_path.cpp",
      "src/engine_adapter/skia_adapter/skia_path_effect.cpp",
      "src/engine_adapter/skia_adapter/skia_picture.cpp",
      "src/engine_adapter/skia_adapter/skia_shader_effect.cpp",
      "src/image/bitmap.cpp",
      "src/image/image.cpp",
      "src/image/picture.cpp",
      "src/text/text.cpp",
      "src/utils/camera3d.cpp",
      "src/utils/matrix.cpp",
    ]

    sources += [
      "$rosen_text_root/properties/font_collection_txt.cpp",
      "$rosen_text_root/properties/placeholder_run.cpp",
      "$rosen_text_root/properties/rosen_converter_txt.cpp",
      "$rosen_text_root/properties/text_style.cpp",
      "$rosen_text_root/properties/typography_create_txt.cpp",
      "$rosen_text_root/properties/typography_style.cpp",
      "$rosen_text_root/properties/typography_txt.cpp",
      "$rosen_text_root/ui/font_collection.cpp",
      "$rosen_text_root/ui/typography.cpp",
      "$rosen_text_root/ui/typography_create.cpp",
    ]

    include_dirs = [
      "include",
      "src",
      "src/engine_adapter",
      "$rosen_2d_engine_root",
      "//foundation/arkui/ace_engine/frameworks",
      "//third_party/bounds_checking_function/include",
    ]

    defines += [ "USE_ACE_SKIA" ]

    if (platform == "ohos") {
      external_deps = [
        "c_utils:utils",
        "hilog_native:libhilog",
        "multimedia_image_standard:image_native",
      ]
      deps = [
        "$ace_flutter_engine_root:flutter_engine_fml_$platform",
        "//foundation/graphic/graphic_2d/rosen/build/flutter/icu:rosen_libicu_$platform",
        "//foundation/graphic/graphic_2d/rosen/build/flutter/libtxt:thirdparty_lib_txt_rosen_$platform",
        "//third_party/bounds_checking_function:libsec_static",
      ]
      include_dirs += [
        "$flutter_root/engine",
        "$flutter_root/engine/flutter/third_party/txt/src",
      ]
      defines += [ "SUPPORT_OHOS_PIXMAP" ]
    } else {
      cflags = [ "-Wno-c++17-extensions" ]

      if (enable_ng_build) {
        deps = [
          "$ace_flutter_engine_root_new/third_party/icu:ace_libicu_dev_$platform",
          "$ace_flutter_engine_root_new/third_party/libtxt:thirdparty_lib_txt_dev_$platform",
          "$ace_flutter_engine_root_new/third_party/skia:ace_skia_dev_$platform",
        ]
        include_dirs += [
          "$flutter_root_new/engine",
          "$flutter_root_new/engine/flutter/third_party/txt/src",
        ]
        defines += [ "USE_CANVASKIT0310_SKIA" ]
      } else {
        deps = [
          "$ace_flutter_engine_root:flutter_engine_fml_$platform",
          "$ace_flutter_engine_root/icu:ace_libicu_$platform",
          "$ace_flutter_engine_root/libtxt:thirdparty_lib_txt_$platform",
          "$ace_flutter_engine_root/skia:ace_skia_$platform",
        ]
        include_dirs += [
          "$flutter_root/engine",
          "$flutter_root/engine/flutter/third_party/txt/src",
        ]
      }

      if (!is_cross_platform_build) {
        deps += [ "//base/hiviewdfx/hilog/frameworks/libhilog:libhilog_source_$platform" ]
      }
    }

    part_name = "graphic_standard"
    subsystem_name = "graphic"
  }
}

foreach(item, ace_platforms) {
  graphics2d_source_set("2d_graphics_source_" + item.name) {
    platform = item.name
    defines = []

    if (defined(item.config)) {
      config = item.config
    } else {
      config = {
      }
    }

    if (defined(config.defines)) {
      defines += config.defines
    }

    if (defined(config.cflags_cc)) {
      cflags_cc = config.cflags_cc
    } else {
      cflags_cc = []
    }
  }
}

if (!is_cross_platform_build) {
  ## Build 2d_graphics.so
  ohos_shared_library("2d_graphics") {
    symlink_target_name = [ "libnative_drawing.so" ]
    deps = [ ":2d_graphics_source_ohos" ]
    part_name = "graphic_standard"
    subsystem_name = "graphic"
  }
}

## Build 2d_graphics.so
ohos_shared_library("2d_graphics_canvaskit0310") {
  sources = [
    "src/draw/brush.cpp",
    "src/draw/color.cpp",
    "src/draw/core_canvas.cpp",
    "src/draw/path.cpp",
    "src/draw/pen.cpp",
    "src/effect/color_filter.cpp",
    "src/effect/color_space.cpp",
    "src/effect/filter.cpp",
    "src/effect/image_filter.cpp",
    "src/effect/mask_filter.cpp",
    "src/effect/path_effect.cpp",
    "src/effect/shader_effect.cpp",
    "src/engine_adapter/impl_factory.cpp",
    "src/engine_adapter/skia_adapter/skia_bitmap.cpp",
    "src/engine_adapter/skia_adapter/skia_camera.cpp",
    "src/engine_adapter/skia_adapter/skia_canvas.cpp",
    "src/engine_adapter/skia_adapter/skia_color_filter.cpp",
    "src/engine_adapter/skia_adapter/skia_color_space.cpp",
    "src/engine_adapter/skia_adapter/skia_image.cpp",
    "src/engine_adapter/skia_adapter/skia_image_filter.cpp",
    "src/engine_adapter/skia_adapter/skia_impl_factory.cpp",
    "src/engine_adapter/skia_adapter/skia_mask_filter.cpp",
    "src/engine_adapter/skia_adapter/skia_matrix.cpp",
    "src/engine_adapter/skia_adapter/skia_paint.cpp",
    "src/engine_adapter/skia_adapter/skia_path.cpp",
    "src/engine_adapter/skia_adapter/skia_path_effect.cpp",
    "src/engine_adapter/skia_adapter/skia_picture.cpp",
    "src/engine_adapter/skia_adapter/skia_shader_effect.cpp",
    "src/image/bitmap.cpp",
    "src/image/image.cpp",
    "src/image/picture.cpp",
    "src/text/text.cpp",
    "src/utils/camera3d.cpp",
    "src/utils/matrix.cpp",
  ]

  defines = [ "USE_CANVASKIT0310_SKIA" ]

  include_dirs = [
    "include",
    "src",
    "src/engine_adapter",
  ]

  if (!is_cross_platform_build) {
    deps = [
      "//foundation/graphic/graphic_2d:libsurface",
      # "//third_party/skia_canvaskit0310:skia",
    ]
  }

  external_deps = [
    "c_utils:utils",
    "hilog_native:libhilog",
    "multimedia_image_standard:image_native",
  ]

  part_name = "graphic_standard"
  subsystem_name = "graphic"
}
## Build 2d_graphics.so
