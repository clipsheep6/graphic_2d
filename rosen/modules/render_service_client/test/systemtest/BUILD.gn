# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/test.gni")

module_out_path = "graphic_standard/render_service_client"

group("systemtest") {
  testonly = true

  deps = [ ":RSInterfacesSystemTest" ]
}

## Build RSInterfacesSystemTest
ohos_systemtest("RSInterfacesSystemTest") {
  module_out_path = module_out_path
  sources = [ "rs_interfaces_system_test.cpp" ]
  deps = [ ":rs_client_test_common" ]
}

## Build rs_client_test_common.a {{{
config("rs_client_test_common_public_config") {
  include_dirs = [
    "//foundation/graphic/graphic_2d/frameworks/surface/include",
    "//foundation/graphic/graphic_2d/rosen/modules/render_service_client/core",

    # windowmanager
    "//foundation/windowmanager/dm/include",
    "//foundation/windowmanager/dmserver/include",
    "//foundation/windowmanager/interfaces/innerkits/dm",
    "//foundation/windowmanager/interfaces/innerkits/wm",
    "//foundation/windowmanager/utils/include",
  ]

  cflags = [
    "-Wall",
    "-Werror",
    "-g3",
    "-Dprivate=public",
    "-Dprotected=public",
  ]
}

ohos_static_library("rs_client_test_common") {
  visibility = [ ":*" ]
  testonly = true

  sources = [ "rs_interfaces_test_utils.cpp" ]

  public_configs = [ ":rs_client_test_common_public_config" ]

  public_deps = [
    "//base/hiviewdfx/hilog/interfaces/native/innerkits:libhilog",
    "//foundation/distributedschedule/samgr/interfaces/innerkits/samgr_proxy:samgr_proxy",
    "//foundation/graphic/graphic_2d:libsurface",
    "//foundation/graphic/graphic_2d/rosen/modules/render_service_base:librender_service_base",
    "//foundation/graphic/graphic_2d/rosen/modules/render_service_client:librender_service_client",
    "//foundation/windowmanager/dm:libdm",
    "//foundation/windowmanager/dmserver:libdms",
    "//foundation/windowmanager/utils:libwmutil",
    "//foundation/windowmanager/wm:libwm",
    "//foundation/windowmanager/wmserver:libwms",
    "//third_party/googletest:gtest_main",
    "//utils/native/base:utils",
  ]
  subsystem_name = "graphic"
  part_name = "graphic_standard"
}
## Build rs_client_test_common.a }}}
