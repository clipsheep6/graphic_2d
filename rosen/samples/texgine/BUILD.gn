# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")

template("ohos_prebuilt_etcs") {
  group_sources = []
  foreach(source, invoker.sources) {
    source_file = get_path_info(source, "file")
    ohos_prebuilt_etc(source_file) {
      source = source
      relative_install_dir = "test_resources"
      part_name = "graphic_standard"
      subsystem_name = "graphic"
    }
    group_sources += [ ":${source_file}" ]
  }
  group(target_name) {
    public_deps = group_sources
  }
}

ohos_prebuilt_etcs("texgine_sample_resources") {
  sources = [
    "resources/Roboto-Black.ttf",
    "resources/seguiemj.ttf",
  ]
}

ohos_executable("texgine_sample") {
  install_enable = true

  sources = [
    "feature_test/feature_test_framework.cpp",
    "texgine_sample.cpp",
  ]

  sources += [
    "feature_test/align_test.cpp",
    "feature_test/alignment_test.cpp",
    "feature_test/bug1.cpp",
    "feature_test/calibrate_position_test.cpp",
    "feature_test/decoration_test.cpp",
    "feature_test/dynamic_font_test.cpp",
    "feature_test/ellipsis_bug1.cpp",
    "feature_test/ellipsis_test.cpp",
    "feature_test/emoji_test.cpp",
    "feature_test/feature_test.cpp",
    "feature_test/font_style_test.cpp",
    "feature_test/glyph_position_test.cpp",
    "feature_test/intrinsic_width_test.cpp",
    "feature_test/layout_test.cpp",
    "feature_test/mix_test.cpp",
    "feature_test/shadow_test.cpp",
    "feature_test/spacing_test.cpp",
    "feature_test/strut_test.cpp",
    "feature_test/text_color_test.cpp",
    "feature_test/weight_style_test.cpp",
    "feature_test/word_boundary_test.cpp",
  ]

  defines = [ "RESOURCE_PATH_PREFIX=\"/system/etc/test_resources/\"" ]

  deps = [
    ":libskia_rs_framework",
    ":texgine_sample_resources",
    "//foundation/graphic/graphic_2d/rosen/modules/texgine:libtexgine",
  ]

  external_deps = [ "hitrace_native:hitrace_meter" ]

  part_name = "graphic_standard"
  subsystem_name = "graphic"
}

config("libskia_rs_framework_config") {
  visibility = [ ":*" ]

  cflags = [
    "-Wall",
    "-Werror",
    "-g3",
  ]
}

config("libskia_rs_framework_public_config") {
  include_dirs = [ "." ]
}

ohos_shared_library("libskia_rs_framework") {
  sources = [ "skia_rs_framework.cpp" ]

  configs = [ ":libskia_rs_framework_config" ]

  public_configs = [ ":libskia_rs_framework_public_config" ]

  public_deps = [
    "//foundation/graphic/graphic_2d/rosen/modules/render_service_client:librender_service_client",
    "//third_party/flutter/build/skia:ace_skia_ohos",
  ]

  external_deps = [
    "eventhandler:libeventhandler",
    "hilog_native:libhilog",
    "hitrace_native:hitrace_meter",
    "input:libmmi-client",
  ]

  part_name = "graphic_standard"
  subsystem_name = "graphic"
}
